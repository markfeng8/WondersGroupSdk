apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 23
        versionName "1.8.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // 所有资源名称添加前缀 wonders_group_ (图片需手动添加前缀)
    //resourcePrefix 'wonders_group_'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // include '*.aar' 此种方式会将 libs 中的 aar 包打进 aar 包中，但注意他不会将 values 中的资源合并进去，所以会有问题
    //implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    //implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // 以下为需要添加的依赖库
    implementation 'com.lzy.net:okgo:3.0.4'
    implementation 'com.orhanobut:logger:2.2.0'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.mylhyl:zxingscanner:2.1.3'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'pub.devrel:easypermissions:2.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.github.iwgang:countdownview:2.1.3'
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:gridlayout-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.44'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'
    //implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0-alpha2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.yanzhenjie:permission:2.0.0-rc12'
    implementation 'com.alibaba:fastjson:1.2.49'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'

//    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:5.1.6'
//    implementation 'com.squareup.retrofit2:retrofit-mock:2.0.2'
//    implementation 'com.squareup.okhttp3:mockwebserver:3.8.1'

    // jar 包会自动打进 aar 包中
    compileOnly files('libs/libammsdk.jar')
    implementation files('libs/WDPay-3.6.0.jar')
    implementation files('libs/UPPayAssistEx.jar')
    implementation files('libs/UPPayPluginExPro.jar')
    implementation files('libs/alipaySdk-20160120.jar')

    // 此种方式不会将 libs 中的 aar 包打进 aar 包中
    implementation(name: 'PALiveDetect1.0.0', ext: 'aar')
    implementation(name: 'zj_essc_sdk-1.0.5', ext: 'aar')
}

/**
 * 自定义生成 aar 包的 task
 */
task makeAar(type: Copy) {
    // 先删除两个旧的 aar 包的目录
    doFirst {
        File file1 = new File('../SDK_Library/build/aar/')
        file1.deleteDir()
        File file2 = new File('../SDK_Library/build/outputs/aar/')
        file2.deleteDir()
    }

    // 复制并重命名文件
    from '../SDK_Library/build/outputs/aar/'
    into '../SDK_Library/build/aar/'
    // 使用闭包映射文件名
    rename { String fileName ->
        fileName.replace('SDK_Library', "hzsdk-${releaseTime()}")
    }
}

/**
 * makeAar task 依赖 assemble
 */
makeAar.dependsOn(assemble)

/**
 * 获取当前的 UTC 时间
 */
static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

/*
 * 打开Terminal输入命令:
 * ./gradlew assemble
 * 生成 aar 包(打包前修改versionCode & versionName)
 * ./gradlew makeAar （替换 wxpi 包名）
 * 查看 app 依赖库关系：
 * ./gradlew dependencies app:dependencies
 * 合并dev分支，请注意--no-ff参数，表示禁用Fast forward
 * git merge --no-ff -m "merge with no-ff" dev
 * git log看看分支历史：
 * git log --graph --pretty=oneline --abbrev-commit
 * git branch -d feature1
 */
