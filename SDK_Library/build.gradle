apply plugin: 'com.android.library'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 3
        versionName "1.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a'
            //, 'x86', 'armeabi-v7a', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            //zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //resourcePrefix 'wonders_group_' // 所有资源名称添加前缀 wonders_group_ (图片需手动添加前缀)

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        //checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // 以下为需要添加的依赖库
    implementation 'com.lzy.net:okgo:3.0.4'
    implementation 'com.orhanobut:logger:2.1.1'
    implementation 'com.mylhyl:zxingscanner:1.5.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'pub.devrel:easypermissions:1.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.github.iwgang:countdownview:2.1.3'
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:recyclerview-v7:27.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.flyco.tablayout:FlycoTabLayout_Lib:2.1.2@aar'

    // jar 包会自动打进 aar 包中
    api files('libs/libammsdk.jar')
    api files('libs/WDPay-3.6.0.jar')
    api files('libs/UPPayAssistEx.jar')
    api files('libs/UPPayPluginExPro.jar')
    api files('libs/alipaySdk-20160120.jar')
}

// 自定义 task，复制并重命名文件
task copyAndRenameFile(type: Copy) {
    from '../SDK_Library/build/outputs/aar/'
    into '../SDK_Library/build/aar/'
    // 使用闭包映射文件名
    rename { String fileName ->
        fileName.replace('SDK_Library', "hzsdk-${releaseTime()}")
    }
}

// copyAndRenameFile task 依赖 assemble
copyAndRenameFile.dependsOn(assemble)

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

/*
 * 打开Terminal输入命令:
 * ./gradlew assemble
 * 生成 aar 包
 * ./gradlew copyAndRenameFile
 * 查看 app 依赖库关系：
 * ./gradlew dependencies app:dependencies
 * 合并dev分支，请注意--no-ff参数，表示禁用Fast forward
 * git merge --no-ff -m "merge with no-ff" dev
 * git log看看分支历史：
 * git log --graph --pretty=oneline --abbrev-commit
 * git branch -d feature1
 */
